import java.util.Locale

plugins {
    alias(libs.plugins.agp.app)
}

extensions.configure<com.android.build.api.dsl.ApplicationExtension> {
    defaultConfig {
        multiDexEnabled = false
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles("proguard-rules.pro")
        }
    }
    namespace = "moe.shimmerfly.shimmerpatch.metaloader"
}

androidComponents.onVariants { variant ->
    val variantCapped = variant.name.replaceFirstChar { it.uppercase() }
    val variantLowered = variant.name.lowercase()
    val buildDirProvider = layout.buildDirectory
    val dexDirProvider = if (variant.buildType == "release") {
        buildDirProvider.dir("intermediates/dex/$variantLowered/minify${variantCapped}WithR8")
    } else {
        buildDirProvider.dir("intermediates/dex/$variantLowered/mergeDex$variantCapped")
    }
    val copyDestination = rootProject.layout.projectDirectory.dir("out/assets/${variant.name}/shimmerpatch")
    // Captured as a plain string so the task does not hold on to the project.
    val outDirLabel = rootProject.layout.projectDirectory.dir("out").asFile.path

    val copyDexTask = tasks.register<Copy>("copyDex$variantCapped") {
        dependsOn("assemble$variantCapped")
        from(dexDirProvider)
        rename("classes.dex", "metaloader.dex")
        into(copyDestination)
    }

    tasks.register("copy$variantCapped") {
        dependsOn(copyDexTask)
        doLast {
            println("Loader dex has been copied to $outDirLabel")
        }
    }
}

dependencies {
    compileOnly("vector:stubs")
    implementation(projects.share.java)
    implementation(libs.hiddenapibypass)
}
